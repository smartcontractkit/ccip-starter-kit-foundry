// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

import {BasicMessageReceiver} from "./BasicMessageReceiver.sol";

import {FinalityCodec} from "@chainlink/contracts-ccip/contracts/libraries/FinalityCodec.sol";
import {Ownable, Ownable2Step} from "@openzeppelin/contracts@5.3.0/access/Ownable2Step.sol";

/**
 * THIS IS AN EXAMPLE CONTRACT THAT USES HARDCODED VALUES FOR CLARITY.
 * THIS IS AN EXAMPLE CONTRACT THAT USES UN-AUDITED CODE.
 * DO NOT USE THIS CODE IN PRODUCTION.
 */
contract BasicMessageReceiverWithCCVs is BasicMessageReceiver, Ownable2Step {
    /// @notice CCV configuration for a source chain.
    /// @dev For incoming messages, this receiver will require this CCV criteria to be met.
    /// Required CCVs must all pass verification. >= optionalThreshold of the optional CCVs must pass verification.
    struct CCVConfig {
        address[] requiredCCVs;
        address[] optionalCCVs;
        uint8 optionalThreshold;
    }

    /// @notice Arguments required to add a CCV configuration for a source chain.
    struct CCVConfigArgs {
        address[] requiredCCVs;
        address[] optionalCCVs;
        uint64 sourceChainSelector;
        uint8 optionalThreshold;
    }

    error DuplicateCCV(uint64 sourceChainSelector, address ccv);
    error InvalidOptionalThreshold(uint64 sourceChainSelector, uint8 optionalThreshold);
    error ZeroAddressNotAllowedAsOptional();

    event CCVConfigSet(
        uint64 indexed sourceChainSelector, address[] requiredCCVs, address[] optionalCCVs, uint8 optionalThreshold
    );
    event MinBlockDepthSet(uint64 indexed sourceChainSelector, uint16 minBlockDepth);

    mapping(uint64 sourceChainSelector => CCVConfig ccvConfig) internal s_ccvConfigs;
    mapping(uint64 sourceChainSelector => uint16 minBlockDepth) internal s_minBlockDepths;

    constructor(address router) BasicMessageReceiver(router) Ownable(msg.sender) {}

    /// @notice Set minimum accepted block depth for a source chain.
    /// @dev 0 means Default Finality is required for that source chain.
    ///      Non-zero values allow Faster Than Finality with a minimum required depth - WARNING only use Faster Than Finality
    ///      when you use a trusted sender on the source chain that manages the finality risk when sending messages.
    function setMinBlockDepth(uint64 sourceChainSelector, uint16 minBlockDepth) external onlyOwner {
        s_minBlockDepths[sourceChainSelector] = minBlockDepth;
        emit MinBlockDepthSet(sourceChainSelector, minBlockDepth);
    }

    /// @notice Returns CCV config and allowed finality for a source chain (see `CCIPReceiver.getCCVsAndFinalityConfig`).
    /// @dev Maps stored min block depth to `FinalityCodec` encoding (0 depth => wait for full finality).
    function getCCVsAndFinalityConfig(
        uint64 sourceChainSelector,
        bytes calldata /*sender*/
    )
        external
        view
        override
        returns (
            address[] memory requiredCCVs,
            address[] memory optionalCCVs,
            uint8 optionalThreshold,
            bytes4 allowedFinalityConfig
        )
    {
        CCVConfig memory config = s_ccvConfigs[sourceChainSelector];
        uint16 minBlockDepth = s_minBlockDepths[sourceChainSelector];
        allowedFinalityConfig = FinalityCodec._encodeBlockDepth(minBlockDepth);
        return (config.requiredCCVs, config.optionalCCVs, config.optionalThreshold, allowedFinalityConfig);
    }

    /// @notice Set CCV configurations for source chains.
    /// @param ccvConfigsToSet List of CCV configs to set.
    function applyCCVConfigUpdates(CCVConfigArgs[] calldata ccvConfigsToSet) external virtual onlyOwner {
        for (uint256 i = 0; i < ccvConfigsToSet.length; ++i) {
            CCVConfigArgs memory args = ccvConfigsToSet[i];
            // If optionalThreshold > optionalCCVs.length, then it's impossible to satisfy the optional CCV requirement.
            // If optionalThreshold == optionalCCVs.length, then optional CCVs are essentially required, they should instead
            // be defined as required CCVs.
            if (args.optionalCCVs.length > 0) {
                if (args.optionalThreshold >= args.optionalCCVs.length) {
                    revert InvalidOptionalThreshold(args.sourceChainSelector, args.optionalThreshold);
                }
            } else {
                if (args.optionalThreshold > 0) {
                    revert InvalidOptionalThreshold(args.sourceChainSelector, args.optionalThreshold);
                }
            }
            uint256 requiredCCVLength = args.requiredCCVs.length;
            uint256 optionalCCVLength = args.optionalCCVs.length;
            uint256 totalCCVLength = requiredCCVLength + optionalCCVLength;
            for (uint256 j = 0; j < totalCCVLength; ++j) {
                address ccvAddressJ =
                    j < requiredCCVLength ? args.requiredCCVs[j] : args.optionalCCVs[j - requiredCCVLength];
                // address(0) is a valid required CCV address, but not a valid optional CCV address.
                // This is because address(0) signals to always enforce the default CCVs for the lane.
                if (j >= requiredCCVLength && ccvAddressJ == address(0)) {
                    revert ZeroAddressNotAllowedAsOptional();
                }

                for (uint256 k = j + 1; k < totalCCVLength; ++k) {
                    address ccvAddressK =
                        k < requiredCCVLength ? args.requiredCCVs[k] : args.optionalCCVs[k - requiredCCVLength];
                    if (ccvAddressK == ccvAddressJ) {
                        revert DuplicateCCV(args.sourceChainSelector, ccvAddressK);
                    }
                }
            }
            s_ccvConfigs[args.sourceChainSelector] = CCVConfig({
                requiredCCVs: args.requiredCCVs,
                optionalCCVs: args.optionalCCVs,
                optionalThreshold: args.optionalThreshold
            });
            emit CCVConfigSet(args.sourceChainSelector, args.requiredCCVs, args.optionalCCVs, args.optionalThreshold);
        }
    }
}
